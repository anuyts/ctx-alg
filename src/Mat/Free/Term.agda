{-# OPTIONS --cubical --type-in-type #-}

open import Cubical.Foundations.Everything renaming (Iso to _â‰…_)
open import Cubical.Data.List
open import Cubical.Data.List.Properties
open import Cubical.Data.List.FinData renaming (lookup to _!_)
open import Cubical.Data.Prod
open import Cubical.Data.W.Indexed
open import Cubical.Data.FinData
open import Cubical.Data.Sum
open import Cubical.Data.Sum as âŠ
open import Cubical.Data.Empty
open import Cubical.Data.Nat
open import Cubical.Foundations.Structure
open import Cubical.Categories.Category
open import Cubical.Categories.Functor renaming (ğŸ™âŸ¨_âŸ© to ftrId)
open import Cubical.Categories.Instances.Sets
open import Cubical.Categories.Constructions.TypeProduct
open import Cubical.Categories.Monad.Base
open import Cubical.Categories.NaturalTransformation.Base

open import Mat.Signature
open import Mat.Free.Presentation

module Mat.Free.Term {sign : Signature} (fmat : PresentationF sign) where

open _â‰…_
open Functor
open Signature sign
open PresentationF fmat

data TermF (X : MType) : (sortOut : Sort) â†’ Type
isSetTermF : (msetX : MSet) (sortOut : Sort) â†’ isSet (TermF (mtyp msetX) sortOut)

msetTermF : MSet â†’ MSet
fst (msetTermF msetX sortOut) = TermF (mtyp msetX) sortOut
snd (msetTermF msetX sortOut) = isSetTermF msetX sortOut

data TermF X where
  varF : âˆ€ {sortOut} â†’ X sortOut â†’ TermF X sortOut
  astF : âˆ€ {sortOut} â†’ Term1 (TermF X) sortOut â†’ TermF X sortOut

RepTermF : (X : MType) (sortOut : Sort) â†’ Type
RepTermF X sortOut =
  IW (Î» sort â†’ X sort âŠ Operation sort)
    (Î» sort â†’ âŠ.elim (Î» v â†’ âŠ¥) Î» o â†’ Fin (length (arity o)))
    (Î» sort â†’ âŠ.elim (Î» v ()) (Î» o p â†’ arity o ! p))
    sortOut

toRepTermF : (X : MType) (sortOut : Sort) â†’ TermF X sortOut â†’ RepTermF X sortOut
toRepTermF X sortOut (varF v) = node (inl v) (Î» ())
toRepTermF X sortOut (astF (term1 o args)) =
  node (inr o) Î» p â†’ toRepTermF X (arity o ! p) (args p)

fromRepTermF : (X : MType) (sortOut : Sort) â†’ RepTermF X sortOut â†’ TermF X sortOut
fromRepTermF X sortOut (node (inl v) u) = varF v
fromRepTermF X sortOut (node (inr o) args) = astF (term1 o Î» p â†’ fromRepTermF X (arity o ! p) (args p))

fromToRepTermF : (X : MType) (sortOut : Sort) (t : TermF X sortOut)
  â†’ fromRepTermF X sortOut (toRepTermF X sortOut t) â‰¡ t
fromToRepTermF X sortOut (varF v) = refl
fromToRepTermF X sortOut (astF (term1 o args)) i =
  astF (term1 o Î» p â†’ fromToRepTermF X (arity o ! p) (args p) i)

toFromRepTermF : (X : MType) (sortOut : Sort) (rt : RepTermF X sortOut)
  â†’ toRepTermF X sortOut (fromRepTermF X sortOut rt) â‰¡ rt
toFromRepTermF X sortOut (node (inl v) u) = cong (node (inl v)) (funExt (Î» ()))
toFromRepTermF X sortOut (node (inr o) args) i =
  node (inr o) (Î» p â†’ toFromRepTermF X (arity o ! p) (args p) i)

isoRepTermF : (X : MType) (sortOut : Sort) â†’ TermF X sortOut â‰… RepTermF X sortOut
fun (isoRepTermF X sortOut) = toRepTermF X sortOut
inv (isoRepTermF X sortOut) = fromRepTermF X sortOut
rightInv (isoRepTermF X sortOut) = toFromRepTermF X sortOut
leftInv (isoRepTermF X sortOut) = fromToRepTermF X sortOut

pathRepTermF : (X : MType) (sortOut : Sort) â†’ TermF X sortOut â‰¡ RepTermF X sortOut
pathRepTermF X sortOut = ua (isoToEquiv (isoRepTermF X sortOut))

isSetRepTermF : (msetX : MSet) (sortOut : Sort) â†’ isSet (RepTermF (mtyp msetX) sortOut)
isSetRepTermF msetX sortOut = isOfHLevelSuc-IW 1 (Î» sort â†’ isSetâŠ (str (msetX sort)) isSetOperation) sortOut

isSetTermF msetX sortOut = substâ» isSet (pathRepTermF (mtyp msetX) sortOut) (isSetRepTermF msetX sortOut)

mapTermF : âˆ€ {X Y} â†’ (âˆ€ sort â†’ X sort â†’ Y sort) â†’ âˆ€ sort â†’ TermF X sort â†’ TermF Y sort
mapTermF f sort (varF x) = varF (f sort x)
mapTermF f sort (astF (term1 o args)) = astF (term1 o Î» p â†’ mapTermF f (arity o ! p) (args p))

mapTermF-id : âˆ€ {X} â†’ mapTermF (Î» sort â†’ idfun (X sort)) â‰¡ (Î» sort â†’ idfun (TermF X sort))
mapTermF-id i sort (varF x) = varF x
mapTermF-id i sort (astF (term1 o args)) = astF (term1 o (Î» p â†’ mapTermF-id i (arity o ! p) (args p)))

mapTermF-âˆ˜ : âˆ€ {X Y Z : MType} â†’ (g : âˆ€ sort â†’ Y sort â†’ Z sort) â†’ (f : âˆ€ sort â†’ X sort â†’ Y sort) â†’
  mapTermF (Î» sort â†’ g sort âˆ˜ f sort) â‰¡ (Î» sort â†’ mapTermF g sort âˆ˜ mapTermF f sort)
mapTermF-âˆ˜ g f i sort (varF x) = varF (g sort (f sort x))
mapTermF-âˆ˜ g f i sort (astF (term1 o args)) = astF (term1 o (Î» p â†’ mapTermF-âˆ˜ g f i (arity o ! p) (args p)))

ftrTermF : Functor catMSet catMSet
F-ob ftrTermF = msetTermF
F-hom ftrTermF = mapTermF
F-id ftrTermF = mapTermF-id
F-seq ftrTermF f g = mapTermF-âˆ˜ g f

-- It's a monad

open NatTrans

Î·TermF : NatTrans (ftrId catMSet) ftrTermF
N-ob Î·TermF msetX sortOut = varF
N-hom Î·TermF {msetX} {msetY} f = refl

joinTermF : âˆ€ {X} sort â†’ TermF (TermF X) sort â†’ TermF X sort
joinTermF sort (varF t) = t
joinTermF sort (astF (term1 o args)) = astF (term1 o (Î» p â†’ joinTermF (arity o ! p) (args p)))

joinTermF-nat : âˆ€ {X Y : MType} f sort â†’ (t : TermF (TermF X) sort)
  â†’ joinTermF {X = Y} sort (mapTermF (mapTermF f) sort t) â‰¡ mapTermF f sort (joinTermF sort t)
joinTermF-nat f sort (varF t) = refl
joinTermF-nat f sort (astF (term1 o args)) i = astF (term1 o Î» p â†’ joinTermF-nat f (arity o ! p) (args p) i)

Î¼TermF : NatTrans (funcComp ftrTermF ftrTermF) ftrTermF
N-ob Î¼TermF msetX = joinTermF
N-hom Î¼TermF {msetX} {msetY} f = funExt Î» sort â†’ funExt Î» t â†’ joinTermF-nat f sort t

open IsMonad

monadTermF : IsMonad ftrTermF
Î· monadTermF = Î·TermF
Î¼ monadTermF = Î¼TermF
idl-Î¼ monadTermF = makeNatTransPathP (Î» i â†’ F-rUnit i) (Î» i â†’ ftrTermF) refl
idr-Î¼ monadTermF = makeNatTransPathP (Î» i â†’ F-lUnit i) (Î» i â†’ ftrTermF) lemma
  where lemma : (Î» msetX sort t â†’ joinTermF sort (mapTermF (Î» sortOut â†’ varF) sort t)) â‰¡
                (Î» msetX sort t â†’ t)
        lemma i msetX sort (varF x) = varF x
        lemma i msetX sort (astF (term1 o args)) = astF (term1 o Î» p â†’ lemma i msetX (arity o ! p) (args p))
assoc-Î¼ monadTermF = makeNatTransPathP (Î» i â†’ F-assoc i) (Î» i â†’ ftrTermF) lemma
  where lemma : (Î» msetX sort t â†’ joinTermF sort (mapTermF joinTermF sort t)) â‰¡
                (Î» msetX sort t â†’ joinTermF sort (joinTermF sort t))
        lemma i msetX sort (varF ttx) = joinTermF sort ttx
        lemma i msetX sort (astF (term1 o args)) = astF (term1 o Î» p â†’ lemma i msetX (arity o ! p) (args p))
